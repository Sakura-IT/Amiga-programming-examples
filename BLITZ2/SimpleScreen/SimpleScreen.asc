```
; Simple Screen 
;
; Author: asman
; Copyrigt: public domain
;
; Exit: left mouse button

;-----------------------------------------------------------------------------

Statement ScreenInit{}
  ;create bitmap number 0 width 320, height 256 with depth 2 (4 colors)
  BitMap 0,320,256,2

  ;initialize palette object to hold 4 colors, all colors set to black.
  InitPalette 0,4

  ;initialize copper list with
  ; ypos = 44, height 256, type is 2 bitplanes,
  ; sprites = 8, 4 colors, no customs
  InitCopList 0,44,256,$00002,8,4,0

  ;disable system
  BLITZ

  ;set palette
  PalRGB 0,0,0,0,0   ;black
  PalRGB 0,1,10,0,10 ;purple
  PalRGB 0,2,10,0,0  ;red
  PalRGB 0,3,0,0,10  ;blue
  
  ;set up screen display
  CreateDisplay 0
  
  ;copy color information to copperlist
  DisplayPalette 0,0
  
  ;copy bitmap information to copperlist
  DisplayBitMap 0,0
End Statement

;-----------------------------------------------------------------------------
;
; main
;

ScreenInit{}

Repeat

  ;wait for vblank
  VWait
  
  ;put purple pixel on random position
  x = Rnd(320)
  y = Rnd(256)
  Plot x,y,1
  
Until Joyb(0) ; wait for left mouse button
```



